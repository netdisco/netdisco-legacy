<h3>Device Port Control</h3>
% $m->comp('SELF:switch') if $cmd =~ /^(up|down)$/;
% $m->comp('SELF:log') if $cmd eq 'log';
% $m->comp('SELF:err') if $cmd eq 'err';
% $m->comp('SELF:reasons') unless $cmd eq 'err';
<%args>
$cmd  => undef
$type => undef
$ip   => undef
$port => undef
$log  => undef
$log_num => 5
$reason => ''
</%args>
<%shared>
# scope
my ($arg_ip,$arg_port,$log,$arg_cmd,$port,$err,$db_port,$arg_reason,$arg_log_num);
my $user      = $m->session->{user};
my $port_ctl  = $m->session->{user_port_ctl};
my $secure    = $r->subprocess_env('https');
my $userip    = $r->connection->remote_ip; 
my $odd = 1 ;
my %Reasons = ( 
                ''            => ['Select a Reason',''],
                'address'     => ['Address Allocation Abuse',
                                  'A system which does not obtain and/or use its address in a legitimate
                                   fashion.  This includes machines that self-assign IP addresses and those
                                   that modify their MAC address.'],
                'dos'         => ['Denial Of Serivce',
                                  'Any kind of data flow, mailicious or otherwise, which is involved
                                   in a service-affecting disruption.  This includes activities such
                                   as ICMP and UDP floods, SYN attacks, and other service disruptive
                                   flows.  A system involved in a DoS attack is usually compromised.'],
                'bandwidth'   => ['Excessive BandWidth',
                                  'A user has sent/received data in excess of campus acceptable use standards.'],
                'compromised' => ['System Compromised',
                                  'The system has been compromised and poses a high risk to campus
                                   resources. If other behavior is observed, such as involvement in a DoS attack, that reason code should be used in place of this one.'],
                'copyright'   => ['Copyright Violation',
                                  "Following the takedown provision of the DMCA to limit the University's copyright liability."],
                'exploit'     => ['Remote Exploit Possible',
                                  'A remotely exploitable vulnerability posing high risk exists on the system.'],
                'polling'     => ['Excessive Polling of DNS/DHCP/SNMP',
                                  'Distinct from DoS attacks, excessive polling is often due to
                                   misconfigured systems or malfunctioning protocol stacks.  An example of
                                   this would be sustained, repetitive polling of the DHCP server for an
                                   address.'],
                'other'       => ['Other', 'Does not fit in any other catagory.  Make a <i>very</i> detailed <TT>Log</TT> entry.']
              );
</%shared>
<%init>
# Check Authorization - User in portcontrol list in config file, and connected via https
unless ($secure eq 'on' and $port_ctl and $cmd =~ /^(up|down|log|err|switch)$/) {
    my $url = $m->interp->apply_escapes( $r->uri, 'u' );
    $m->redirect("login.html?done=$url");
}

$arg_cmd = $cmd;  $arg_ip = $ip;  $arg_port = $port; 
$arg_reason = $reason; $arg_log_num = $log_num;

# Check that port exists in netdisco
$db_port = sql_hash('device_port',['up','up_admin','descr','name','mac','remote_ip','remote_port'],
                {'ip'=>$ip, 'port'=>$port});

if (!defined $db_port){
    $cmd = 'err';
    $err = "Port [ $ip / $port ] not found in netdisco.";
} elsif (defined $db_port->{remote_ip}){
    $cmd = 'err';
    $err = "Port [ $ip / $port ] is an uplink port.  Control from netdisco not allowed."; 
} else {
        # Check for port alias/dns
        my $dns = sql_scalar('device_ip',['dns'],{'ip'=>$ip,'port'=>$port});
        $dns = sql_scalar('device',['dns'],{'ip'=>$ip}) unless defined $dns;
        $db_port->{dns} = $dns;
}

# Don't require log entry, only reason code.
#if ($cmd eq 'switch' and (!defined $log or !length($log)) ){
#    $cmd = 'err';
#    $err = "You must add a log entry!";    
#}
if ($cmd eq 'switch' and (!$reason) ){
    $cmd = 'err';
    $err .= "You must select a reason for bringing the port $type\n";
}

if ($cmd eq 'switch' and $type =~ /^(up|down)$/) {
    my $dir = $type;
    $dir = 'enable' if $type eq 'up';
    $dir = 'disable' if $type eq 'down';
    
    # Switch
    my $rv = system('/usr/local/netdisco/port_control',$ip,$port,$type);
    $rv /= 256;

    # Switch Failed
    if ($rv) {
        $cmd = 'err';
        $err = "Switching of port failed. ($rv)\n";
        # Log anyways.
        $log .= " [FAILED] Could not bring port $type. ($rv)"; $type = 'fail';

    # Switch Succeeded - Update netdisco
    } else {  
        insert_or_update('device_port', {'ip'=>$ip,'port'=>$port} , {'up_admin'=>$type});
        $cmd = 'log';

        # Send E-Mail to Abuse
        my $portctl_email = $netdisco::CONFIG{portctl_email};
        # Pretty Print
        if (defined $portctl_email){
            my $dns = $db_port->{dns};
            my $body = <<"end_body";
........ n e t d i s c o .........
  Device : $dns ($ip) 
  Port   : $port
  Action : $dir
  User   : $user \@ $userip 
  Reason : [$reason] - $Reasons{$reason}->[0]
  Log    : $log

end_body
            mail($portctl_email,"port $dir $dns($ip)/$port",$body);
        }
    }

    # Log attempt 
    insert_or_update('device_port_log', {},
                     {'ip'=>$ip,'port'=>$port,'log'=>$log,'userip'=>$userip,
                      'username'=>$user,'action'=>$dir, 'reason' => $reason }
                    ); 
}

</%init>
<%method log>
% my $logs = sql_rows('device_port_log', ['id','log','reason','username','userip','action','extract(epoch from creation) as creation'] , 
%       {'ip'=>$arg_ip, 'port'=>$arg_port} );
% my $count = 0;
<h4>Log Entries for <A HREF="device.html?ip=<%$arg_ip |u%>&port=<% $arg_port|u%>"><%$arg_ip%>/<%$arg_port%></A>
<& SELF:back &></h4>
<FORM ACTION="<%$r->uri%>" METHOD=GET>
<INPUT TYPE="HIDDEN" NAME="cmd" VALUE="log">
<INPUT TYPE="HIDDEN" NAME="ip" VALUE="<%$arg_ip |h%>">
<INPUT TYPE="HIDDEN" NAME="port" VALUE="<%$arg_port|h%>">
<TABLE WIDTH=85% CELLSPACING=0 CELLPADDING=2 BORDER=0>
<TR>
    <TH>User</TH>
    <TH>Action</TH>
    <TH>Date</TH>
    <TH>Reason</TH>
    <TH>Log</TH>
</TR>
% foreach my $entry (sort {$b->{creation} <=> $a->{creation} } @$logs) {
<TR CLASS="match-<% ++$odd % 2 %>">
    <TD><%$entry->{username}%> @ <%$entry->{userip}%></TD>
    <TD><%$entry->{action}%></TD>
    <TD><%scalar localtime($entry->{creation})%></TD>
    <TD><A HREF="#<%$entry->{reason}%>"><%$Reasons{$entry->{reason}}->[0]%></A></TD>
    <TD><TEXTAREA ROWS=3 COLS=30 WRAP=soft><%$entry->{log}%></TEXTAREA>
    </TD>
</TR>

% $count++;
% last unless ($arg_log_num eq 'All' or $count < $arg_log_num);
%}
</TABLE>
Log Entires to Show : <SELECT NAME="log_num">
% foreach my $num (qw/1 3 5 10 20 50 100 All/){
    <OPTION VALUE="<%$num%>" <%$num eq $arg_log_num ? 'SELECTED' : ''%>><%$num%>
%  }
</SELECT>
<INPUT TYPE="SUBMIT" VALUE="Refresh">
</FORM>
</%method>
<%method switch>
<FORM ACTION="<%$r->uri%>" METHOD="post">
<INPUT TYPE="HIDDEN" NAME="cmd" VALUE="switch">
<INPUT TYPE="HIDDEN" NAME="type" VALUE="<%$arg_cmd |h%>">
<INPUT TYPE="HIDDEN" NAME="port" VALUE="<%$arg_port |h%>">
<INPUT TYPE="HIDDEN" NAME="ip" VALUE="<%$arg_ip |h%>">
<TABLE BORDER=0 CELLSPACING=0 CLASS="box">
<TR CLASS="match-1">
    <TD>Port :</TD>
    <TD CLASS="tr_data-1"><A HREF="device.html?ip=<%$arg_ip |u%>&port=<% $arg_port|u%>"><% $arg_ip %>[<% $arg_port %>]</A></TD>
</TR>
<TR CLASS="match-0">
    <TD>User :</TD>
    <TD CLASS="tr_data-0"><U><%$user%></U> at <%$userip%> <BR><u><% scalar localtime %></u>
    </TD>
</TR>
<TR CLASS="match-1">
    <TD>Reason :<BR>&nbsp;</TD>
    <TD CLASS="tr_data-1"><SELECT NAME="reason" SIZE=1>
% $arg_reason = 'other' if $arg_cmd eq 'up';
% foreach my $this_r (sort keys %Reasons){
        <OPTION VALUE="<%$this_r |h%>" <%$this_r eq $arg_reason ? 'SELECTED' : ''%>> \
%   if ($this_r){        
[<%$this_r%>] \
%   }
<%$Reasons{$this_r}->[0]%> 
%}
    </SELECT>
    <BR><FONT SIZE=-3>See below for descriptions.</FONT>
    </TD>
</TR>
<TR CLASS="match-0">
    <TD>Log:</TD>
    <TD CLASS="tr_data-0"><TEXTAREA NAME="log" ROWS=3 COLS=40 WRAP=soft></TEXTAREA>
    <BR>Enter a useful description why the port is being turned <% $arg_cmd eq 'down' ? 'off' : 'on' %>.
    </TD>
</TR>
<TR CLASS="match-1">
    <TD COLSPAN=2 CLASS="tr_data-1"><INPUT TYPE="submit" VALUE="Turn Port <%$arg_cmd eq 'down' ? 'off' : 'on'%>">
        <P><I>Note</I>: This can take up to 1 minute.  Do not hit refresh on your browser.
    </TD>
</TR>
</TABLE>
</FORM>
<& SELF:back &>    
</%method>
<%method reasons>
<h4>Reason Descriptions</h4>
<TABLE BORDER=0 CELLSPACING=0 CELLPADDING=3>
<TR>
    <TH>Reason</TH>
    <TH>Description</TH>
</TR>
<TR>
    <TD COLSPAN=2><HR NOSHADE></TD>
</TR>
% foreach my $this_r (sort keys %Reasons){
%    next unless $this_r;
%    my $long = $Reasons{$this_r}->[1];
%    $long =~ s!\s+! !g;
<TR>
    <TD><A NAME="<%$this_r%>"><B><%$this_r%></B></A></TD>
    <TD><%$long%></TD>
</TR>
% }
</TABLE>
</%method>
<%method err>
<% $err %>
    <& SELF:back &>
</%method>
<%method back>
<FORM ACTION="device.html" METHOD="get">
<INPUT TYPE="hidden" NAME="ip" VALUE="<%$arg_ip |h%>">
<INPUT TYPE="hidden" NAME="port" VALUE="all">
<INPUT TYPE="submit" VALUE="Back to Device View">
</FORM>
</%method>
<%method title>
- Port Control \
</%method>
%# $Id$
%# vim:syntax=mason
