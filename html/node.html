<FORM ACTION="<%$r->uri%>" METHOD="get">
<& SELF:matches &>

<h3>Search for a Node</h3>
Enter a MAC, Hostname or IP:<INPUT TYPE="text" name="node" maxlength=20 size=20 VALUE="<%$node%>"><BR>
Time Stamps: 
<INPUT TYPE="radio" NAME="showdates" VALUE="1" <%$showdates ? 'CHECKED' : '' %>>On
<INPUT TYPE="radio" NAME="showdates" VALUE="0" <%$showdates ? '' : 'CHECKED' %>>Off 
<BR>
Archived Data:
<INPUT TYPE="checkbox" NAME="archives" VALUE="1" <%$archives ? 'CHECKED' : '' %>
<BR>
Show Vendor :
<INPUT TYPE="radio" NAME="showoui" VALUE="1" <%$showoui ? 'CHECKED' : ''%>>On
<INPUT TYPE="radio" NAME="showoui" VALUE="0" <%$showoui ? '' : 'CHECKED' %>>Off
<BR>
<INPUT TYPE="submit" Value="Search">
<A HREF="<%$r->uri%>">[Clear]</A>
</FORM>

<h3>Advanced Search</h3>

<BR>
Search:
<UL>
<LI>by OUI
<LI>by Partial MAC Address
<LI>for Wireless Access Points (APs)
</UL>
<P>
<FORM METHOD="GET" ACTION="node_search.html">
<INPUT TYPE="submit" VALUE="Advanced Search">

<P>
* <font size=-2>Advanced Search takes a few seconds to load.</font>
</FORM>
<%args>
$node => undef
$showdates => 1
$archives => 0
$showoui => 1
</%args>
%#
<%method title>
- Node Search\
</%method>
%#
<%shared>
my @matches = ();
my $badmatch = 0;
my $dates = 0;
my $archive = 0;
my $oui = 0;
my $domain = $netdisco::CONFIG{domain};
</%shared>
%#
<%init>
# Pass the form argument to the global for <%methods>
$dates = $showdates;
$archive = (defined $archives and $archives == 1) ? 1 : 0;
$oui = $showoui;

# Search by MAC
if (defined $node){
    $m->flush_buffer;
    $node =~ s/^\s+//;
    $node =~ s/\s+$//;

    # Translate space separated octets into colon separated.
    my $hex = "[0-9a-fA-F]";
    if ($node =~ /^${hex}{2} ${hex}{2} ${hex}{2} ${hex}{2} ${hex}{2} ${hex}{2}$/){
        $node =~ s/\s+/:/g;
    }
}
my $is_mac = defined $node ? &is_mac($node) : 0;
if (defined $node and $is_mac) {
    my $where = { 'mac' => $node };
    $where->{'active'} = 1 unless $archive;

    # Lookup IPs associated with this MAC
    my $ips = sql_rows('node_ip',
                ['mac','ip','active','extract(epoch from time_first) as time_first', 'extract(epoch from time_last) as time_last'],
                $where
                );

    foreach my $node_ip (@$ips){
        $node_ip->{type} = 'ip';
        $node_ip->{host} = hostname($node_ip->{ip});
        $node_ip->{host} =~ s/\Q$domain\E//;
        $node_ip->{oui}  = substr($node_ip->{mac},0,8);
        push (@matches,$node_ip);
    }

    # Lookup what switch ports this MAC was seen at.
    my $ports = sql_rows('node',
                ['mac','switch','port','active','oui','extract(epoch from time_first) as time_first', 'extract(epoch from time_last) as time_last'],
                $where
                );   

    foreach my $port (@$ports) {
        $port->{type} = 'port';
        #TODO - Switch to db call
        $port->{switchname} = &hostname($port->{switch});
        $port->{switchname} =~ s/\Q$domain\E//;
        push (@matches,$port);
    }

    # Check the posibilty that this could be a switch port's MAC
    my $switch_ports = sql_rows('device_port', 
            ['ip','port','mac','extract(epoch from creation) as time_first', 'extract(epoch from creation) as time_last'],
            {'mac' => $node});

    foreach my $sp (@$switch_ports){
        $sp->{type} = 'swport';
        $sp->{active} = 1;
        $sp->{oui}  = substr($sp->{mac},0,8);
        $sp->{switchname} = sql_scalar('device',['dns'],{'ip' => $sp->{ip}});
        $sp->{switchname} =~ s/\Q$domain\E//;
        push (@matches,$sp);
    }
}

# Search by HOST 
if (defined $node and !$is_mac) {
    my $hostname = &hostname($node);
    $hostname =~ s/\Q$domain\E//;
    my $realip = &getip($node);
    # see if this is a listed device or one of its aliases.
    my $device = root_device($realip);

    # Find MACs associated with this IP
    my $where = { 'ip'=>$realip};
    $where->{'active'} = 1 unless $archive;
    my $macs = sql_rows('node_ip',
                ['mac','ip','active','extract(epoch from time_first) as time_first', 'extract(epoch from time_last) as time_last'],
                $where
                       );

    my @foundmacs = ();

    # Add info to each entry
    foreach my $node_ip (@$macs){
        $node_ip->{host} = $hostname;
        $node_ip->{type} = 'host_ip';
        $node_ip->{dev}  = $device;
        $node_ip->{oui}  = substr($node_ip->{mac},0,8);
        push (@matches,$node_ip);
        push (@foundmacs, $node_ip->{mac});
    }

    # Get details for each MAC associated with this IP
    foreach my $mac (@foundmacs){
        $where = { 'mac' => $mac };
        $where->{'active'} = 1 unless $archive;
        my $ports = sql_rows('node',
                    ['mac','switch','port','active','oui','extract(epoch from time_first) as time_first', 'extract(epoch from time_last) as time_last'],
                    $where
                        );   
        foreach my $port (@$ports) {
            $port->{type} = 'port';
            # TODO - switch this to a db lookup,
            $port->{switchname} = &hostname($port->{switch});
            $port->{switchname} =~ s/\Q$domain\E//;
            push(@matches,$port);
        }
    }

    # If the IP belongs to a device, but we have no info, must be a VLAN or unused ALIAS
    if (!scalar @matches and $device) {
        my $entry = {};
        $entry->{type} = 'alias';
        $entry->{mac} = $realip;
        $entry->{active} = 1;
        $entry->{dev}  = $device;
        $entry->{dns}  = sql_scalar('device',['dns'],{'ip'=>$device});
        $entry->{dns}  =~ s/\Q$domain\E//;
        $entry->{ip} = $realip;
        push (@matches,$entry);
    }
    
}

# Lookup OUI for matches
my %ouis;
foreach my $match (@matches){
    #my $mac = $match->{mac};
    #my $oui = substr(uc($mac),0,8);
    my $oui = uc($match->{oui});
    # only look each up once.
    unless ( defined $ouis{$oui} ){
        $ouis{$oui} = sql_scalar('oui',['company'],{'oui' => $oui});
    }
    $match->{oui} = $ouis{$oui};
}

if ($node and ! scalar @matches ){
    $badmatch = $node;
    $node = undef;
}
</%init>
%#
%# matches() - Outputs each match depending on type
%#
<%method matches>
% return unless (scalar @matches or $badmatch);
<h3>Search Results</H3>
% if (! scalar(@matches) and $badmatch) {
Did not find anything for <% $badmatch %>.
% return; }
<TABLE cellspacing=0 cellpadding=4 border=0 width=100%>
<TR>
    <TH>MAC</TH>
% if ($oui) {
    <TH>NIC Vendor</TH>
%}
    <TH>Match</TH>
    <TH>Device or Node</TH>
% if ($dates) {
    <TH>First Seen</TH>
    <TH>Last Seen</TH>
% }
</TR>
% my $odd=0;
% foreach my $match (@matches) {
%     my $mac = $match->{mac};
%     my $active = $match->{active};
%     next if (!$active and !$archive);
<TR CLASS="match-<% ++$odd % 2 %>">
<TD>
    <A HREF="node.html?node=<% $mac |u %>"><%$mac%></A>
    <% $active ? '' : '<SUP>*</SUP>' %>
</TD>
% if ($oui){
    <TD><%$match->{oui}%></TD>
%}
% if ($match->{type} eq 'ip') {
    <TD>MAC -> IP</TD>
    <TD><A HREF="node.html?node=<% $match->{ip} |u %>"><% $match->{ip} %></A>
    (<% defined $match->{host} ? $match->{host} : '[No DNS]' %> )
    </TD>
% } elsif ($match->{type} eq 'port') { 
    <TD>Switch Port</TD>
    <TD>
    <A HREF="device.html?ip=<% $match->{switch} |u%>&port=<% $match->{port} |u%>"><% $match->{switch} %> [ <% $match->{port} %> ]</A>
    (<% $match->{switchname} ? $match->{switchname} : '[No DNS]' %>)
    </TD>
%} elsif ($match->{type} =~ /^host/ and defined $match->{dev}) {
    <TD>IP -> Device</TD>
    <TD><A HREF="device.html?ip=<% $match->{dev} |u %>"><% $match->{ip} %></A>
    (<% defined $match->{host} ? $match->{host} : '[No DNS]' %>)
    </TD>
% } elsif ($match->{type} =~ /^host/){
    <TD>IP -> MAC</TD>
    <TD><A HREF="node.html?node=<% $match->{ip} |u %>"><% $match->{ip} %></A>
    (<% defined $match->{host} ? $match->{host} : '[No DNS]' %>)
    </TD>
% } elsif ($match->{type} eq 'swport'){
    <TD>Switch Port</TD>
    <TD><A HREF="device.html?ip=<%$match->{ip}|u%>&port=<%$match->{port}|u%>"><%$match->{switchname}%> / <%$match->{port}%></A></TD>
% } elsif ($match->{type} eq 'alias'){
    <TD>Device IP w/out MAC</TD>
    <TD><A HREF="device.html?ip=<%$match->{dev}|u%>"><%$match->{dev}%> \
(<% defined $match->{dns} ? $match->{dns} : '[No DNS]' %>)</A></TD>
%} else  {
    <TD>Bad Match</TD>
    <TD>Type : <%$match->{type}%></TD>
%}
% if ($dates) {
%   my $time_first = $match->{time_first} ? localtime($match->{time_first}) : '&nbsp;';
%   my $time_last = $match->{time_last} ? localtime($match->{time_last}) : '&nbsp;';
    <TD><% $time_first %></TD>
    <TD><% $time_last %></TD>
% }

</TD></TR>
% }
</TABLE>
% $m->out("* - Denotes archived data.") if $archive;
</%method>
%# $Id$
%# vim:syntax=mason
